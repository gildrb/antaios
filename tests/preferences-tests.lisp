(in-package #:antaios)

;;;; -- Durable Settings Tests --

(-> preferences-tests--without-model-environment (function) t)
(defun preferences-tests--without-model-environment (function)
  "Call FUNCTION while model, effort, and Fast mode overrides are absent."
  (with-test-environment (("ANTAIOS_MODEL" nil)
                          ("ANTAIOS_REASONING_EFFORT" nil)
                          ("ANTAIOS_CODEX_FAST_MODE" nil))
    (funcall function)))

(-> preferences-tests--create (configuration &rest t) configuration)
(defun preferences-tests--create (configuration &rest overrides)
  "Create a process configuration on CONFIGURATION's roots, reading its preferences."
  (apply #'configuration-create
         :source-root (config :source-root configuration)
         :working-directory (config :working-directory configuration)
         :config-root (config :config-root configuration)
         :data-root (config :data-root configuration)
         :state-root (config :state-root configuration)
         :cache-root (config :cache-root configuration)
         :codex-auth-path (config :codex-auth-path configuration)
         :grok-bootstrap-auth-path (config :grok-bootstrap-auth-path configuration)
         overrides))

(-> preferences-tests--file-plist (configuration) (values list boolean))
(defun preferences-tests--file-plist (configuration)
  "Return the durable plist in CONFIGURATION's preferences file and whether it is version 8."
  (multiple-value-bind (form sole-form-p)
      (snapshot-read (configuration-preferences-path configuration))
    (values (preferences--form->plist form)
            (and sole-form-p (eql (getf (rest form) :version) 8) t))))

(-> preferences-tests--warned-p (function) boolean)
(defun preferences-tests--warned-p (function)
  "Return true when FUNCTION signals a preferences load warning."
  (let ((warned-p nil))
    (handler-bind ((preferences-load-warning
                     (lambda (warning)
                       (setf warned-p t)
                       (muffle-warning warning))))
      (funcall function))
    warned-p))

(-> test-preferences () null)
(defun test-preferences ()
  "Test the preferences file persists, merges, rejects unsupported versions, and recovers.

Source precedence, source recording, and value rejection belong to setinka."
  (preferences-tests--without-model-environment
   (lambda ()
     (with-test-configuration (configuration)
       (let ((pathname (configuration-preferences-path configuration)))
         (test-assert
          (equal pathname
                 (merge-pathnames "preferences.sexp" (config :state-root configuration)))
          "global preferences live under the state root")
         (let ((created (preferences-tests--create configuration)))
           (dolist (case '((:reasoning-traces-p nil) (:compact-view-p t)
                           (:turn-timestamps-p nil) (:cache-miss-notices-p t)
                           (:simple-technical-english-p t)
                           (:session-title-generation-p t) (:fullscreen-p t)
                           (:codex-fast-mode-p nil) (:permission-mode nil)))
             (destructuring-bind (name expected) case
               (test-assert (eq (config name created) expected)
                            (format nil "a missing file leaves ~(~A~) at its default" name))))
           (test-assert (string= (config :model created) *default-model*)
                        "a missing file leaves the default model")
           (test-assert (not (probe-file pathname))
                        "reading defaults does not create the file")
           (setf (config :reasoning-traces-p created) t)
           (multiple-value-bind (plist version-8-p)
               (preferences-tests--file-plist configuration)
             (test-assert (and version-8-p (eq (getf plist :reasoning-traces-p) t))
                          "a durable change writes a version 8 record"))
           (setf (config :model created) "gpt-5.6-luna"
                 (config :reasoning-effort created) "high"
                 (config :permission-mode created) ':ask)
           (let ((reloaded (preferences-tests--create configuration)))
             (test-assert (and (config :reasoning-traces-p reloaded)
                               (string= (config :model reloaded) "gpt-5.6-luna")
                               (string= (config :reasoning-effort reloaded) "high")
                               (eq (config :permission-mode reloaded) ':ask))
                          "durable values survive into a new configuration"))
           (setf (config :permission-mode created) nil)
           (test-assert (null (config :permission-mode (preferences-tests--create configuration)))
                        "an optional choice can be unset durably")
           (test-assert
            (handler-case (progn (setf (config :permission-mode created) ':sandboxed) nil)
              (setting-invalid () t))
            "session-only permission modes cannot be saved"))
         (with-test-environment (("ANTAIOS_MODEL" "gpt-5.6-terra")
                                 ("ANTAIOS_CODEX_FAST_MODE" "on"))
           (let ((created (preferences-tests--create configuration)))
             (test-assert (string= (config :model created) "gpt-5.6-terra")
                          "ANTAIOS_MODEL supplies the model")
             (test-assert (config :codex-fast-mode-p created)
                          "ANTAIOS_CODEX_FAST_MODE supplies Codex Fast mode")))
         (snapshot-write pathname
                         '(:preferences :version 8
                           :model "gpt-5.6-typo" :reasoning-effort "bogus"
                           :compact-view-p nil :future-key 7))
         (let ((created (preferences-tests--create configuration)))
           (test-assert (string= (config :model created) *default-model*)
                        "an unsupported durable model is dropped")
           (test-assert (string= (config :reasoning-effort created)
                                 *default-reasoning-effort*)
                        "an unsupported durable effort is dropped")
           (setf (config :turn-timestamps-p created) t)
           (let ((plist (preferences-tests--file-plist configuration)))
             (test-assert (and (= (getf plist :future-key) 7)
                               (not (getf plist :compact-view-p))
                               (eq (getf plist :turn-timestamps-p) t))
                          "storing one value keeps unknown and unrelated keys"))
           (snapshot-write pathname '(:preferences :version 8 :cache-miss-notices-p t))
           (setf (config :fullscreen-p created) nil)
           (let* ((plist (preferences-tests--file-plist configuration))
                  (fullscreen (member :fullscreen-p plist)))
             (test-assert (and (eq (getf plist :cache-miss-notices-p) t)
                               fullscreen
                               (null (second fullscreen))
                               (null (member :turn-timestamps-p plist)))
                          "storing merges into the file as another process left it")))
          (dolist (version '(1 2 3 4 5 6 7 9))
            (let ((form (list ':preferences ':version version
                              ':model "gpt-5.6-luna"
                              ':reasoning-effort "high"))
                  (created nil))
              (snapshot-write pathname form)
              (test-assert
               (preferences-tests--warned-p
                (lambda () (setf created (preferences-tests--create configuration))))
               "an unsupported preference version is reported and ignored")
              (test-assert (string= (config :model created) *default-model*)
                           "an unsupported file leaves the default model in place")
              (test-assert (equal (snapshot-read pathname) form)
                           "loading an unsupported file preserves its stored data")))
         (with-open-file (stream pathname :direction ':output :if-exists ':supersede
                                          :external-format ':utf-8)
           (write-string "(:preferences :version 8 :model" stream))
         (let ((created nil))
           (test-assert
            (preferences-tests--warned-p
             (lambda () (setf created (preferences-tests--create configuration))))
            "a truncated file is reported and ignored")
           (setf (config :compact-view-p created) nil)
           (test-assert (equal (preferences-tests--file-plist configuration)
                               '(:compact-view-p nil))
                        "the next store replaces a damaged file"))
    nil)))))
